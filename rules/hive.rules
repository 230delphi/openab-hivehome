//hiveHome rules, based on work from speedtest rules.
val String filename = "hiveHome.rules"

rule "hiveHome init"
when
    System started
then
    createTimer(now.plusSeconds(240)) [|
        if (hiveHomeManualRun.state == NULL) hiveHomeManualRun.postUpdate(OFF)
        if (hiveHomeUpdateRunning.state == NULL) hiveHomeUpdateRunning.postUpdate("-")
        //if (hiveHomeSummary.state == NULL || hiveHomeSummary.state == "")
        //    hiveHomeSummary.postUpdate("(unknown)")
    ]
end

rule "hiveHome"
when
    Time cron "0 15 * * * ?" or
    Item hiveHomeManualRun received command ON
then
    logInfo(filename, "--> hiveHome executed...")
    hiveHomeUpdateRunning.postUpdate("Running...")

    // update timestamp for last execution
    hiveHomeLastUpdate.postUpdate(new DateTimeType())

    // execute the script, you may have to change the path depending on your system
    var String hiveHomeCliOutput = executeCommandLine("/etc/openhab2/scripts/hiveToOpenHAB.sh", 60*1000)
	// working dir: /var/lib/openhab2
	// var String hiveHomeCliOutput = executeCommandLine("pwd", 60*1000)

    // for debugging:
    logInfo(filename, "--> hiveHome output:\n" + hiveHomeCliOutput + "\n\n")

    hiveHomeUpdateRunning.postUpdate("Data...")

    // crude error check based on script output
    if (hiveHomeCliOutput.startsWith("OK")) {
        logInfo(filename, "--> hiveHome finished: success")
    } else {
		// optional - set values to null so its clear failure.
        upstairsTargetTemp.postUpdate(0)
        upstairsTemp.postUpdate(0)
        upstairsStatus.postUpdate("OFF")
        downstairsTargetTemp.postUpdate(0)
        downstairsTemp.postUpdate(0)
        downstairsStatus.postUpdate("OFF")
        hotWaterStatus.postUpdate("OFF")
		// set as failed and log error.
        hiveHomeUpdateRunning.postUpdate("Error executing script")
        logError(filename, "--> hiveHome failed. Output:\n" + hiveHomeCliOutput + "\n\n")
    }
    hiveHomeManualRun.postUpdate(OFF)
end
